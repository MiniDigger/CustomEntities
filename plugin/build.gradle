buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "net.onedaybeard.artemis:artemis-odb-gradle-plugin:2.1.0"
    }
}

configurations {
    server {
        transitive = false
    }
    serverPlugin {
        transitive = false
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.11.2-R0.1-SNAPSHOT'
    compileOnly 'com.comphenix.protocol:ProtocolLib:4.2.1'

    compile project(":api")

    compile project(":v1_11_R1")

    server 'org.spigotmc:spigot:1.11.2-R0.1-SNAPSHOT'
    serverPlugin 'com.comphenix.protocol:ProtocolLib:4.2.1'
}

apply plugin: "artemis"

jar {
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

jar.baseName = "CustomEntities"

processResources {
    filesMatching('**/plugin.yml') {
        filter {
            it.replace('@version@', project.getVersion())
        }
    }
}

weave {
    classesDir = sourceSets.main.output.classesDir
    dependsOn build
    enableArtemisPlugin = true
    enablePooledWeaving = true
    generateLinkMutators = true
    optimizeEntitySystems = true
}

task installPlugins(type: Copy) {
    from(configurations.serverPlugin)
    into "$rootDir/testserver/plugins"
    rename("([^-]*).*\\.jar", '$1.jar')
}

task installServer(type: Copy) {
    from(configurations.server)
    into "$rootDir/testserver/"
    rename { 'spigot.jar' }
    installPlugins.execute()
}

task install(type: Copy) {
    from("$rootDir/plugin/build/libs/") {
        include "**/CustomEntities-*.jar"
    }
    from("$rootDir/example-plugin/build/libs/") {
        include "**/ExamplePlugin-*.jar"
    }
    rename("([^-]*).*\\.jar", '$1.jar')
    into "$rootDir/testserver/plugins/"
}


